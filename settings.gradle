rootProject.name = 'correlation-sketches'
include 'core'
include 'benchmark'

dependencyResolutionManagement {
    def tablesawVersion = "0.42.0"
    def tablesawParquetVersion = "0.9.0"
    versionCatalogs {
        libs {
            // testing
            version('junit', '5.8.1')
            library('junit.api', "org.junit.jupiter", "junit-jupiter-api").versionRef('junit')
            library('junit.params', "org.junit.jupiter", "junit-jupiter-params").versionRef('junit')
            library('junit.engine', "org.junit.jupiter", "junit-jupiter-engine").versionRef('junit')
            library('assertj.core', "org.assertj:assertj-core:3.20.2")
            // logging
            version('slf4j', '1.7.32')
            library('slf4j.nop', 'org.slf4j', 'slf4j-nop').versionRef('slf4j')
            library('slf4j.api', 'org.slf4j', 'slf4j-api').versionRef('slf4j')
            library('slf4j.log4j12', 'org.slf4j', 'slf4j-log4j12').versionRef('slf4j')
            // CLI parser
            version('picocli', '4.6.1')
            library('picocli.picocli', 'info.picocli', 'picocli').versionRef('picocli')
            library('picocli.codegen', 'info.picocli', 'picocli-codegen').versionRef('picocli')
            // Others
            library('guava', 'com.google.guava:guava:31.0.1-jre')
            library('smile', 'com.github.haifengl:smile-core:1.5.3')
            library('fastutil', 'it.unimi.dsi:fastutil:8.5.6')
            library('apache.commons.text', 'org.apache.commons:commons-text:1.8')
            library('apache.lucene', 'org.apache.lucene:lucene-core:8.2.0')
            library('jackson.dataformat.csv', 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.13.0')
            library('tablesaw.core', "tech.tablesaw:tablesaw-core:${tablesawVersion}")
            library('tablesaw.parquet', "net.tlabs-data:tablesaw_${tablesawVersion}-parquet:${tablesawParquetVersion}")
            library('kvdb4j', 'io.github.vida-nyu:kvdb4j:0.1.0')
        }
    }
}
