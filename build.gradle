plugins {
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.7'
    id 'com.github.sherter.google-java-format' version '0.9' apply false
}

// Run tests in Intellij using gradle test runner
idea.project.settings {
    delegateActions {
        delegateBuildRunToGradle = true
        testRunner = 'PLATFORM'
    }
}

subprojects {
    group 'corrsketches'

    apply plugin: 'java'
    apply plugin: 'com.github.sherter.google-java-format'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    // Setup for google-java-format plugin for code formatting.
    // To format code, use:
    //     gradle goJF
    // To verify code format compliance, use:
    //      gradle verJF
    googleJavaFormat {
        toolVersion = '1.11.0'
        exclude '.gradle/*'
    }
}
